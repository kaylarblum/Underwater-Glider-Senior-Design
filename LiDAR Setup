// LiDAR set up using an Arduino UNO
// Distance, strength, temperature testing

#include <SoftwareSerial.h>
#include "TFMini.h"

TFMini tfmini;

SoftwareSerial SerialTFMini(5,6);   // pins

// Subfunction to obtain data from LiDAR
void getTFminiData(int* distance, int* strength, int* temperature)  
{
  static char i = 0;
  char j = 0;
  int checksum = 0;
  static int rx[9];

  if (SerialTFMini.available())
  {
    rx[i] = SerialTFMini.read();
    if (rx[0] != 0x59)
    {
      i = 0;
    }
    else if (i == 1 && rx[1] != 0x59)
    {
      i = 0;
    }
    else if (i == 8)
    {
      for (j = 0; j < 8; j++)
      {
        checksum += rx[j];
      }
      if (rx[8] == (checksum % 256))
      {
        *distance = rx[2] + rx[3] * 256;  // mm
        *strength = rx[4] + rx[5] * 256;  // 0-65536 
        *temperature = rx[6] + rx[7] * 256;
      }
      i = 0;
    }
    else
    {
      i++;
    }
  }
}

void setup()  
{
  Serial.begin(115200);                   //Initialize hardware serial port (serial debug port)
  while (!Serial);                        // wait for serial port to connect. Needed for native USB port only
  Serial.println("Initializing...");
  SerialTFMini.begin(TFMINI_BAUDRATE);    //Initialize the data rate for the SoftwareSerial port
  tfmini.begin(&SerialTFMini);            //Initialize the TF Mini sensor

  // Delay after sensor initialization
  delay(1000);  
}


// Main function
void loop()
{
  // Variables
  int distance = 0;                             // sensor distance to object
  int strength = 0;                             // sensor strength
  int temperature = 0;                          // sensor temperature

  while (!distance)
  {
    // Call getTFminiData function and return values
    getTFminiData(&distance, &strength, &temperature);

    // Distance is obtained aka sensor is working
    if (distance)                               
    {

      // Convert temp to C
      temperature = (temperature/8) - 256;

      // Convert distance to underwater distance
      distance = distance * 0.7525;        

      // State distance from obstacle, laser strength, and starting temperature
      Serial.print("\nDistance (cm): ");
      Serial.print("\t\tLaser strength: ");
      Serial.println("\tChip Temperature (C): ");
      Serial.print(distance);
      Serial.print("\t\t\t");
      Serial.print(strength);
      Serial.print("\t\t\t");
      Serial.println(temperature);
    }

  }
}

